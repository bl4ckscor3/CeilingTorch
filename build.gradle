plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'net.neoforged.gradle.userdev' version '[7.0,7.1)'
//	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = '1.27'
group = 'bl4ckscor3.mod.ceilingtorch'

base {
	archivesName = 'ceilingtorch-1.20.2'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
	configureEach {
		systemProperty 'forge.logging.console.level', 'debug'
		modSource project.sourceSets.main
	}

	client {
		workingDirectory project.file('run/client')
	}

	server {
		workingDirectory project.file('run/server')
		programArgument '-nogui'
	}
}

repositories {
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

	maven {
		url "https://maven.theillusivec4.top/"
	}
}

dependencies {
	implementation 'net.neoforged:neoforge:20.2.65-beta'

	implementation "curse.maven:sbm-bone-torch-238695:4883926"
	implementation "curse.maven:tofucraftreload-317469:4883747"
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title": "Ceiling Torch",
                    "Specification-Vendor": "bl4ckscor3",
                    "Specification-Version": "${version}",
                    "Implementation-Title": "Ceiling Torch",
                    "Implementation-Version": "${version}",
                    "Implementation-Vendor": "bl4ckscor3"])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}